//Mzce +2: Stupefier
COPY ~%MOD_FOLDER%/comp/remainder/BZMA41BI.BAM~ ~override/BZMA41BI.BAM~

COPY_EXISTING ~BLUN41.ITM~ ~override/BZMA41B.ITM~ //Mace +1 Stupefier -> +2
    //Patches
        SAY NAME2 @600
        SAY IDENTIFIED_DESC @601
        WRITE_ASCII_TERMINATE 0x3A "BZMA41BI"   // Icon
        WRITE_LONG 0x34 4500                    // Price
        WRITE_LONG 0x42 60                      // Lore to Identify
        WRITE_LONG 0x60 2                       // Enchantment
        LPF ~ALTER_ITEM_HEADER~
            INT_VAR
                header = 1
                thac0_bonus = 2
                damage_bonus = 3
            STR_VAR
                icon = ~BZMA41BI~       // icon to display for melee
        END
        LPF ~ALTER_ITEM_EFFECT~
            INT_VAR
                //NOTE: Match all opcodes
                check_headers = 1
                probability1 = 15
                savebonus = "-1"
        END

//Hammer of Dawn
COPY ~%MOD_FOLDER%/comp/remainder/BZHM13BI.BAM~ ~override/BZHM13BI.BAM~
COPY_EXISTING ~HAMM13.ITM~ ~override/BZHM13B.ITM~ //Hammer of Dawn +1 -> +2
    //Patches
        SAY NAME2 @602
        SAY IDENTIFIED_DESC @603
        WRITE_ASCII_TERMINATE 0x3A "BZHM13BI"   // Icon
        WRITE_LONG 0x34 4500                    // Price
        WRITE_LONG 0x42 60                      // Lore to Identify
        WRITE_LONG 0x60 2                       // Enchantment
        LPF ~ALTER_ITEM_HEADER~
            INT_VAR
                header = 1
                thac0_bonus = 2
                damage_bonus = 3
            STR_VAR
                icon = ~BZHM13BI~       // icon to display for melee
        END
        LPF ~ALTER_ITEM_EFFECT~
            INT_VAR
                check_headers = 1
                match_opcode = 235      // Wing Buffet
                probability1 = 15
        END

//Longsword Harrower
COPY ~%MOD_FOLDER%/comp/remainder/BZSW79BI.BAM~ ~override/BZSW79BI.BAM~
COPY_EXISTING ~SW1H79.ITM~ ~override/BZSW79B.ITM~   //Harrower +1 -> +2
    //Patches
        SAY NAME2 @604
        SAY IDENTIFIED_DESC @605
        WRITE_ASCII_TERMINATE 0x3A "BZSW79BI"   // Icon
        WRITE_LONG 0x34 5000                    // Price
        WRITE_LONG 0x42 60                      // Lore to Identify
        WRITE_LONG 0x60 2                       // Enchantment
        LPF ~ALTER_ITEM_HEADER~
            INT_VAR
                header = 1
                thac0_bonus = 2
                damage_bonus = 2
            STR_VAR
                icon = ~BZSW79BI~       // icon to display for melee
        END
        LPF ~ALTER_ITEM_EFFECT~
            INT_VAR
                check_headers = 1
                match_opcode = 344      // Enchantment vs. Creature Type
                special = 4             // enchantment
        END


//Crossbow of speed +1
COPY ~%MOD_FOLDER%/comp/remainder/BZXB06BI.BAM~ ~override/BZXB06BI.BAM~
COPY_EXISTING ~XBOW06.ITM~ ~override/BZXB06B.ITM~   //Crossbow of speed +1 -> +2
    //Patches
        SAY NAME2 @606
        SAY IDENTIFIED_DESC @607
        WRITE_LONG 0x34 16000                   // Price
        WRITE_ASCII_TERMINATE 0x3A "BZXB06BI"   // Icon
        WRITE_LONG 0x42 80                      // Lore to Identify
        WRITE_LONG 0x60 2                       // Enchantment
        LPF ~ALTER_ITEM_HEADER~
            INT_VAR
                header = 1
                thac0_bonus = 2
                damage_bonus = 2
            STR_VAR
                icon = ~BZXB06BI~       // icon to display for ranged
        END

//Dagger +2: Werebane
COPY ~%MOD_FOLDER%/comp/remainder/BZDG09BI.BAM~ ~override/BZDG09BI.BAM~
COPY_EXISTING ~DAGG05.ITM~ ~override/BZDG09B.ITM~   //Werebane +1 -> +2
    //Patches
        SAY NAME1 @608
        SAY NAME2 @609
        SAY UNIDENTIFIED_DESC @610
        SAY IDENTIFIED_DESC @611
        WRITE_LONG 0x18 0b01101100              // flags: 2-3, 5-6 (6 is newly set)
        WRITE_LONG 0x34 6000                    // Price
        WRITE_LONG 0x38 1                       // Stack Size
        WRITE_ASCII_TERMINATE 0x3A "BZDG09BI"   // Icon: Inventory
        WRITE_LONG 0x42 80                      // Lore to Identify
        WRITE_ASCII_TERMINATE 0x58 "CDAGG04"    // Icon: Description
        WRITE_LONG 0x60 2                       // Enchantment
        LPF ~ALTER_ITEM_HEADER~
            INT_VAR
                header = 1
                thac0_bonus = 2
                damage_bonus = 2
                charges = 0             // make returning
                animation_overhand = 100
                bullet = 0              // make returning
            STR_VAR
                icon = ~BZDG09BI~       // icon to display for ranged
        END
        LPF ~ADD_ITEM_HEADER~       // Add melee header
            INT_VAR
                type = 1                // Melee
                required_id = 1         // Require ID to use?
                location = 1            // Weapon
                alt_dicesize = 0
                target = 1              // Living actor
                target_count = 0
                range = 1               // range 1 ft.
                projectile_type = 0
                alt_dicenumber = 0
                speed = 1
                alt_damage = 0
                thaco = 2
                dicesize = 4
                school = 0
                dicenumber = 1
                sectype = 0
                damage = 2
                damage_type = 1         // Piercing damage
                charges = 0
                depletion = 0
                flags = 1               // STR bonus
                projectile = 1          // None
                //Animation %s
                overhand = 0
                backhand = 20
                thrust = 80
                is_bow = 0
                is_xbow = 0
                is_sling = 0
                copy_header = 0
                insert_point = ~-1~
            STR_VAR
                icon = ~BZDG09BI~
        END
        LPF ~ADD_ITEM_EQEFFECT~         // thac0 vs Lycanthropes
            INT_VAR
                opcode = 177            // Type: Use EFF File
                target = 1              // self
                power = 0
                parameter1 = 0          // IDS Value: Anyone
                parameter2 = 2          // Which IDS: EA.IDS
                timing = 2              // Timing Mode: Instant/While equipped
                resist_dispel = 0       // Natural/nonmagical
                duration = 0
                probability1 = 100
                probability2 = 0
                dicenumber = 0
                dicesize = 0
                savingthrow = 0
                savebonus = 0
                special = 0
                insert_point = "-1"     // add to end
            STR_VAR
                resource = ~LYCNHIT3~   // Which EFF
        END
        LPF ~ADD_ITEM_EQEFFECT~         // enchantment vs lycanthropes
            INT_VAR
                opcode = 344            // Type: Enchantment vs. creature type
                target = 1              // self
                power = 0
                parameter1 = 122        // IDS Value: Lycanthropes
                parameter2 = 4          // Which IDS: RACE.IDS
                timing = 2              // Timing Mode: Instant/While equipped
                resist_dispel = 0       // Natural/nonmagical
                duration = 0
                probability1 = 100
                probability2 = 0
                dicenumber = 0
                dicesize = 0
                savingthrow = 0
                savebonus = 0
                special = 5             // enchantment
                insert_point = "-1"     // add to end
            STR_VAR
                resource = ~~           // unused
        END
        //attack per round
        LPF ~ALTER_ITEM_EFFECT~
            INT_VAR
                match_opcode = 1        // Modify attacks per round
                check_globals = 1
                parameter1 = 1          // number of attacks
                parameter2 = 0          // Increment attacks per round
        END
        //I cannot update based on parameters, so delete these
        LPF ~DELETE_EFFECT~
            INT_VAR
                check_globals  = 1
                match_opcode = 7        // Match Set color
                match_parameter1 = 27
        END
        LPF ~DELETE_EFFECT~
            INT_VAR
                check_globals  = 1
                match_opcode = 7        // Match Set color
                match_parameter1 = 239
        END
        LPF ~ALTER_ITEM_EFFECT~
            INT_VAR
                match_opcode = 7        // Match Set color
                check_globals = 1
                parameter1 = 248        // Color: Ogre Major - Battleship Gray
                parameter2 = 20         // Part: Grip/staff minor
        END
        LPF ~CLONE_EFFECT~
            INT_VAR
                check_globals = 1
                match_opcode = 7        // Match Set color
                parameter1 = 81         // Color: Faded gold
                parameter2 = 16         // Part: Head/blade/staff major
        END
        LPF ~CLONE_EFFECT~
            INT_VAR
                check_globals = 1
                match_opcode = 7        // Match Set color
                match_parameter1 = 248  // barely needed; limit to one clone
                parameter1 = 27         // Color: Gray
                parameter2 = 21         // Part: Head/blade minor
        END
        LPF ~ADD_ITEM_EFFECT~           // Damage vs Lycanthropes
            INT_VAR
                opcode = 177            // Type: Use EFF File
                target = 2              // preset target
                power = 0
                parameter1 = 122        // IDS Value: Lycanthrope
                parameter2 = 4          // Which IDS: RACE.IDS
                timing = 1              // Timing Mode: Instant/Permanent until death
                resist_dispel = 0       // Natural/nonmagical
                duration = 0
                probability1 = 100
                probability2 = 0
                dicenumber = 0
                dicesize = 0
                savingthrow = 0
                savebonus = 0
                special = 0
                insert_point = "-1"     // add to end
                type = 99               // Type of header: All
            STR_VAR
                resource = ~LYCNDAM3~    // Which EFF
        END