/***************************
*   Ankheg Plate Mail +1   *
***************************/
// Ankheg Plate Mail +1 Inventory BAM
COPY ~%MOD_FOLDER%/craftable-items/items/plate-mail/BZPM06AI.BAM~ ~override/BZPM06AI.BAM~

//Ankheg Plate Mail -> +1
COPY_EXISTING ~PLAT06.ITM~ ~override/BZPLM06A.ITM~
    //Patches
        SAY NAME2 @50000
        SAY IDENTIFIED_DESC @50001
        WRITE_LONG 0x18 0b01101100              // flags: 2-3, 5-6 (6 is newly set)
        WRITE_ASCII_TERMINATE 0x3A "BZPM06AI"   // Icon
        WRITE_LONG 0x34 16500                   // Price
        WRITE_LONG 0x42 70                      // Lore to Identify
        //I cannot update based on parameters, so delete instead
        LPF ~DELETE_EFFECT~
            INT_VAR
                check_globals = 1
                match_opcode = 0        // AC bonus
                match_parameter2 = BIT4
        END
        //Re-add the flat AC bonus
        LPF ~ADD_ITEM_EQEFFECT~         // Flat AC bonus
            INT_VAR
                opcode = 0              // Type: AC bonus
                target = 1              // Self
                power = 0
                parameter1 = 0          // Actual AC Bonus
                parameter2 = BIT4       // Set base AC to value
                timing = 2              // Timing Mode: Instant/While Equiped
                resist_dispel = 0       // Natural/nonmagical
                duration = 0
                probability1 = 100
                probability2 = 0
                dicenumber = 0
                dicesize = 0
                savingthrow = 0
                savebonus = 0
                special = 0
                insert_point = "-1"     // add to end
        END